#!/usr/bin/perl
#
# Perl script to convert Pod documentation to HTML templates.
# The modules used to generate the Pod model have since been
# moved into the separate badger-pod sub-project.  They'll
# get release separately as Badger-Docs, Badger-Pod, or 
# something else, or they'll get merged back into the core
# then they're properly done.
#
# Written by Andy Wardley http://wardley.org/
#
# 23 August 2008
#

use strict;
use warnings;
use FindBin '$Bin';
use lib "$Bin/../lib";
use Badger::Filesystem 'Dir';
use Badger::Codecs codecs => 'YAML';
use Badger::Docs;
use Badger::Test
    tests => 8,
    debug => 'Badger::Docs Badger::Docs::Visitor',
    args  => \@ARGV;

my $VERBOSE       = grep(/^--?v(erbose)?$/, @ARGV);
my $DRY_RUN       = grep(/^(-n|--nothing|--dry_run)$/, @ARGV);
my $MKDIR         = grep(/^(-m|--mkdir)$/, @ARGV);
my $root_dir      = Dir($Bin)->parent;
my $lib_dir       = $root_dir->dir('lib')->must_exist;
my $docs_dir      = $root_dir->dir('docs')->must_exist($MKDIR);
my $pages_dir     = $docs_dir->dir('templates', 'pages')->must_exist($MKDIR);
my $meta_dir      = $docs_dir->dir('metadata')->must_exist($MKDIR);
my $index_file    = $meta_dir->file('index.yaml');
my $pages_file    = $meta_dir->file('pages.yaml');
my $sections_file = $meta_dir->file('sections.yaml');
my $modules_file  = $meta_dir->file('modules.yaml');

if ($VERBOSE) {
    print STDERR <<EOF;
    debug: $DEBUG
  verbose: $VERBOSE
  dry_run: $DRY_RUN
     root: $root_dir
      lib: $lib_dir
     docs: $docs_dir
    pages: $pages_dir,
EOF
}

my $docs = Badger::Docs->new( 
    root     => $lib_dir, 
    uri_root => $pages_dir,
#   uri_base => '/docs',
    debug    => $DEBUG,
    verbose  => $VERBOSE,
    dry_run  => $DRY_RUN,
    mkdir    => $MKDIR,
);
ok( $docs, 'Created Badger::Docs object.' );
pass('Visiting documentation tree to generate HTML...');

my $visitor = $docs->visit;
ok( $visitor, 'Visit complete.  HTML documentation generated.' );

$index_file->write( encode_YAML($visitor->index) );
pass( "Wrote URI index to $index_file" );

$pages_file->write( encode_YAML($visitor->pages) );
pass( "Wrote pages metadata to $pages_file" );

$sections_file->write( encode_YAML($visitor->sections) );
pass( "Wrote sections metadata to $sections_file" );

$modules_file->write( encode_YAML($visitor->modules) );
pass( "Wrote modules metadata to $modules_file" );


my @QUOTES = (
    'How about a nice game of tennis?',
    "Let's go forage for nuts and berries!",
    "Have you seen my skateboard?",
);

pass( $QUOTES[int(rand(@QUOTES))] );
